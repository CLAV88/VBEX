VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Composed"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = True
'
' Composed Class
' ==============
'
' Copywrite (C) 2014 Philip Wales
' This file (Composed.cls) is distributed under the GPL-3.0 license
' Obtain a copy of the GPL-3.0 license <http://opensource.org/licenses/GPL-3.0>
'
Option Explicit
Implements Showable
Implements Applicable
'
' Private Members
' ---------------
'
Private pInnerOp As Applicable
Private pOuterOp As Applicable
'
' Constructors
' ------------
'
Public Function Make(ByVal outerOp As Applicable, _
        ByVal innerOp As Applicable) As Composed

    Dim result As New Composed
    Set result.inner = innerOp
    Set result.outer = outerOp
    Set Make = result

End Function
Friend Property Set inner(ByVal innerOp As Applicable)

    Set pInnerOp = innerOp

End Property
Friend Property Set outer(ByVal outerOp As Applicable)

    Set pOuterOp = outerOp

End Property
'
' Applicable
' -----------
'
' ### Meaningful
'
Public Function ApplyOnArray(ByRef args() As Variant) As Variant

    Assign ApplyOnArray, pOuterOp.Apply(pInnerOp.ApplyOnArray(args))

End Function
'
' ### Embedded
'
Private Function Applicable_ApplyOnArray(ByRef args() As Variant) As Variant

    Assign Applicable_ApplyOnArray, ApplyOnArray(args)
   
End Function
'! default member
Public Function Apply(ParamArray args() As Variant) As Variant
Attribute Apply.VB_UserMemId = 0

    Assign Apply, ApplyOnArray(cast.CArray(args))

End Function
Private Function Applicable_Apply(ParamArray args() As Variant) As Variant

    Assign Applicable_Apply, ApplyOnArray(CArray(args))
   
End Function
Public Function Compose(ByVal f As Applicable) As Applicable

    Set Compose = defApply.Compose(Me, f)

End Function
Private Function Applicable_Compose(ByVal f As Applicable) As Applicable

    Set Applicable_Compose = Compose(f)

End Function
Public Function AndThen(ByVal g As Applicable) As Applicable

    Set AndThen = defApply.AndThen(Me, g)

End Function
Private Function Applicable_AndThen(ByVal g As Applicable) As Applicable

    Set Applicable_AndThen = AndThen(g)

End Function
Public Function Partial(ParamArray args() As Variant) As Applicable

    Set Partial = AsPartial(CArray(args))

End Function
Private Function Applicable_Partial(ParamArray args() As Variant) As Applicable

    Set Applicable_Partial = AsPartial(CArray(args))

End Function
Public Function AsPartial(ByRef args() As Variant) As Applicable

    Set AsPartial = defApply.AsPartial(Me, args)

End Function
Private Function Applicable_AsPartial(ByRef args() As Variant) As Applicable

    Set Applicable_AsPartial = AsPartial(args)

End Function
Public Function Delay(ParamArray args() As Variant) As delayed

    Set Delay = AsDelay(CArray(args))

End Function
Private Function Applicable_Delay(ParamArray args() As Variant) As delayed

    Set Applicable_Delay = AsDelay(CArray(args))

End Function
Public Function AsDelay(ByRef args() As Variant) As delayed

    Set AsDelay = defApply.AsDelay(Me, args)

End Function
Private Function Applicable_AsDelay(ByRef args() As Variant) As delayed

    Set Applicable_AsDelay = AsDelay(args)

End Function
'
' Showable
' ---------
'
Public Function Show() As String

    Show = defShow.ParamShowableObject(Me, pOuterOp, pInnerOp)

End Function
Private Function Showable_Show() As String

    Showable_Show = Show

End Function
